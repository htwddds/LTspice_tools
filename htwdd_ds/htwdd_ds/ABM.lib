***********************************************************************
*     MACRO and MODEL LIBRARY for symbol library ABM                  *
***********************************************************************
* 2001 02 (B. Fischer)
* 2015 05 (J. Schoenherr)
*=====================================================================
* voltage controlled switch
*$
.SUBCKT sw_vc_close A B in  
+       PARAMS: Vt   =0    ; threshold voltage
+               Vh   =0,   ; hysteresis voltage
+               Ron  =1,
+               Roff =1Meg
S1 A B in 0 Smod
.model Smod sw (Ron={Ron}, Roff={Roff}, Vt={Vt}, Vh={Vh})
.ENDS
*$
*---------------------------------------------------------------------
* voltage difference controlled switch
.SUBCKT sw_vdc_close A B in1 in2  
+       PARAMS: Vt   =0    ; threshold voltage
+               Vh   =0,   ; hysteresis voltage
+               Ron  =1,
+               Roff =1Meg
S1 A B in1 in2 Smod
.model Smod sw (Ron={Ron}, Roff={Roff}, Vt={Vt}, Vh={Vh})
.ENDS
*$
*---------------------------------------------------------------------
* current controlled switch
.SUBCKT sw_ic_close A B in1 in2
+       PARAMS: It   =0,   ; threshold current
+               Ih   =0mA, ; hysteresis current
+               Ron  =1,
+               Roff =1Meg
W1 A B Vdummy Smod
.model Smod csw (Ron={Ron}, Roff={Roff}, It={It}, Ih={Ih})
Vdummy in1 in2 0
.ENDS
*$
*=====================================================================
* time controlles switches
*---------------------------------------------------------------------
* time controlles on-switch (for DC and AC analyse the switch is off)
.SUBCKT sw_tc_close 1 2 
+       PARAMS: td    =1ms  ; initial delay
+               trf   =1ns  ; rising/falling time, always > 0
+               ton   =1ms  ; time for on
+               tper  =2ms  ; ton+toff+2*trf
+               Ron   =1    ; on  resistor
+               Roff  =1Meg ; off resistor (Roff/Ron < 1E10)
V1 3 0 pulse(0 1 {td} {trf} {trf} {ton} {tper})
S1 1 2 3 0 Smod
.model Smod sw (Ron={Ron}, Roff={Roff}, Vt=0.5, Vh=0)
.ENDS
*$
*---------------------------------------------------------------------
* time controlles off-switch (for DC and AC analyse the switch is off)
.SUBCKT sw_tc_open 1 2
+       PARAMS: td    =1ms  ; initial delay
+               trf   =1ns  ; rising/falling time, always > 0
+               toff  =1ms  ; time for off
+               tper  =2ms  ; ton+toff+2*trf
+               Ron   =1    ; on  resistor
+               Roff  =1Meg ; off resistor (Roff/Ron < 1E10)
V1 3 0 pulse(1 0 {td} {trf} {trf} {toff} {tper})
S1 1 2 3 0 Smod
.model Smod sw (Ron={Ron}, Roff={Roff}, Vt=0.5, Vh=0)
.ENDS
*$
*---------------------------------------------------------------------
* time controlles change-switch (for DC and AC analyse the switch is off)
.SUBCKT sw_tc_change 1 2 3
+       PARAMS: td    =1ms  ; initial delay
+               trf   =1ns  ; rising/falling time, always > 0
+               ton   =1ms  ; time for on (between 1 and 2)
+               tper  =2ms  ; ton+toff+2*trf
+               Ron   =1    ; on  resistor
+               Roff  =1Meg ; off resistor (Roff/Ron < 1E10)
V1 4 0 pulse(-1 1 {td} {trf} {trf} {ton} {tper})
S1 1 2 4 0 Smod
S2 1 3 0 4 Smod
.model Smod sw (Ron={Ron}, Roff={Roff}, Vt=0, Vh=0)
.ENDS
*$
*=====================================================================
.SUBCKT const out
+       PARAMS: val=1.0
V1 out 0 {val}
.ENDS
*---------------------------------------------------------------------
.SUBCKT factor in out
+       PARAMS: fac=10e3
E1 out 0 VALUE {fac*V(in)}
.ENDS
*---------------------------------------------------------------------
.SUBCKT value out
+       PARAMS: val=3.141592653
E1 out 0 VALUE {val}
.ENDS
*=====================================================================
.SUBCKT f(x) in out
+       PARAMS: fcn=f(x)
E1 out 0 VALUE {fcn(V(in))}
.ENDS
*---------------------------------------------------------------------
.SUBCKT f(xy) inx iny out
+       PARAMS: fcn=f(xy)
E1 out 0 VALUE {fcn(V(inx),V(iny))}
.ENDS
*---------------------------------------------------------------------
.SUBCKT ite then else out
+       PARAMS: cond=?
E1 out 0 VALUE {if((cond), v(then), v(else))}
.ENDS
*---------------------------------------------------------------------
.SUBCKT limit in out
+       PARAMS: min =-1
+               max =1
E1 out 0 VALUE {limit(V(in), min, max)}
.ENDS
*=====================================================================
* differentiation
.SUBCKT dxdt in out
E1 out 0  VALUE {DDT(v(in))}
.ENDS
*$
*---------------------------------------------------------------------
* indefinite integral
.SUBCKT integral_i in out
+       PARAMS: yo=0 ; starting condition
E1 out 0  VALUE {yo+SDT(v(in))}
.ENDS
*$
*---------------------------------------------------------------------
* definite integral
.SUBCKT integral_d in out
+       PARAMS: t1=0 ; start time
+               t2=1 ; end time
E1 1   0  VALUE {if((time>=t1 & time<=t2), 1, 0)}
E2 out 0  VALUE {SDT(v(1)*v(in))}
.ENDS
*=====================================================================
.SUBCKT delay in out
+       PARAMS: td =1s   ; delay time
E1 out 0 LAPLACE {V(in)} {1/exp(s*td)}
.ENDS
*---------------------------------------------------------------------
.SUBCKT delay_z in out
+       PARAMS: ts =1s   ; sampling periode
E1 out 0 LAPLACE {V(in)} {1/exp(s*ts)}
.ENDS
*---------------------------------------------------------------------
.SUBCKT laplace in out
+       PARAMS: n0=1 n1=0 n2=0 n3=0 n4=0 n5=0 n6=0 ; numerator
+               d0=1 d1=0 d2=0 d3=0 d4=0 d5=0 d6=0 ; denominator
E1 out 0 LAPLACE {V(in)} {(n0 + (n1*s) + (n2*s^2) + (n3*s^3) + (n4*s^4) + (n5*s^5) + (n6*s^6))/(d0 + (d1*s) + (d2*s^2) + (d3*s^3) + (d4*s^4) + (d5*s^5) + (d6*s^6))}
.ENDS
*=====================================================================
.SUBCKT sum in1 in2 out
E1 out 0  VALUE {V(in1)+V(in2)}
.ENDS
*---------------------------------------------------------------------
.SUBCKT sum3 in1 in2 in3 out
E1 out 0  VALUE {V(in1)+V(in2)+V(in3)}
.ENDS
*---------------------------------------------------------------------
.SUBCKT diff in1 in2 out
E1 out 0 VALUE {V(in2,in1)}
.ENDS
*---------------------------------------------------------------------
.SUBCKT mult in1 in2 out
E1 out 0 VALUE {V(in1)*V(in2)}
.ENDS
*=====================================================================
*$
